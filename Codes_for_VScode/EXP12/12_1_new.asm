DATA SEGMENT
	FREQ DW 660,0FFFFH,660,588,524,588,524,494,440,0FFFFH,440,0FFFFH,440,698,0FFFFH,698,660,588,524,588,698, 660, 698,0FFFFH,698,660,588
	         DW 0FFFFH,588,698, 660,0FFFFH,660,524,440,524, 494,660,588,524,494,524,440,0
	DURA DW 200,10,100,100,100,100,100,100,200,10, 200,10,400,200,10,100,100,100,100,100,100, 800, 200,10,100,100,200
	          DW 10,100,100,200,10,100,100,200,200, 200,200,100,100,100,100, 800,0
	STR DB "SONG OF FOUR SEASON$"
DATA ENDS
	;FREQ 是频率数据，DURA是节拍数据
STACKS SEGMENT PARA STACK
STACKS ENDS
CODE SEGMENT
	ASSUME CS:CODE,DS:DATA,SS:STACKS
SING PROC NEAR 
	PUSH DI
	PUSH SI
	PUSH BP
	PUSH BX		;例行push保护
	LEA SI,FREQ	;将频率信息首地址赋给SI
	LEA BP,DURA	;将节拍时间信息首地址赋给BP
RETP: 	MOV DI,DS:[SI]
	CMP DI,0		;歌曲结尾频率0，用以判断是否结束
	JE ENDSONG
	MOV BX,DS:[BP]
	CALL SOUND	;参数为DI和BX；调用发声子程序
	ADD SI,2		;DW类型，所以要加上2
	ADD BP,2		;DW类型，所以要加上2
	JMP RETP
ENDSONG:POP BX
	POP BP
	POP SI
	POP DI		;例行恢复
	RET
SING ENDP

SOUND PROC NEAR
	PUSH AX		;例行push保护
	PUSH BX		;BX节拍时间数据
	PUSH CX
	PUSH DX
	PUSH DI		;入口参数DI给定频率数据
	MOV AL,0B6H	;8253初始化(通道2,方式3,产生方波信号)
	OUT 43H,AL	;43H端口是8253的命令寄存器
	MOV DX,12H	;计算时间常数
	MOV AX,34DCH
	DIV DI
	OUT 42H,AL	;给8253通道2设置计数初值
	MOV AL,AH
	OUT 42H,AL
	IN  AL,61H 	;读8255B口
	MOV AH,AL
	OR AL,3		;8255 PB1PB0置1,开喇叭 0000 0011
	OUT 61H,AL
DELAY: 	MOV CX,10000
DL10ms: 	LOOP DL10ms	;延时10ms
	DEC BX		;BX—节拍时间对应10ms的倍数,如:BX=100,节拍时间=10ms*100=1s
	JNZ DELAY
	MOV AL,AH
	OUT 61H,AL	;8255 PB1PB0恢复为零,关喇叭
	POP DI
	POP DX
	POP CX
	POP BX
	POP AX		;例行pop恢复
	RET
SOUND  ENDP

START:
	MOV AX,DATA
	MOV DS,AX
	MOV AX,STACKS
	MOV SS,AX	;初始化
	LEA DX,STR
        	MOV AH,09H
	INT 21H	;显示歌曲名字
	CALL SING
	MOV AH,4CH
	INT 21H	;调用SING子程序随后退回主程序
CODE ENDS
	END START
